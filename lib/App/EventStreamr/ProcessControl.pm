package App::EventStreamr::ProcessControl;

use v5.010;
use strict;
use warnings;
use Method::Signatures; # libmethod-signatures-perl
use Proc::Daemon; # libproc-daemon-perl
use Proc::ProcessTable; # libproc-processtable-perl

use Moo::Role;

# ABSTRACT: A process role

# VERSION: Generated by DZP::OurPkg:Version

=head1 SYNOPSIS

  This is a role and  shouldn't be directly instantiated.

=head1 DESCRIPTION

  This is a Role that can be consumed to provide process operations
  used throughout EventStreamr.

=cut

requires 'status','command','id','config';

has 'pid' => ( is => 'rw', lazy => 1, builder => 1, clearer => 'clear_pid' );

method _build_pid() {
  my $pt = Proc::ProcessTable->new;
  my @procs = grep { $_->cmndline =~ /$self->{command}/ } @{ $pt->table };

  if (@procs) {
    $self->{status}{$self->{id}}{pid} = $procs[0]->pid;
    $self->{status}{$self->{id}}{running} = 1;
    return $self->{status}{$self->{id}}{pid};
  } else {
    $self->{status}{$self->{id}}{running} = 0;
    return 0;
  }
}

method start() {
  my %proc_opts = ( exec_command => $self->command );
 
  #XXX: Proc::Daemon will exit parent in Void 
  my $state = Proc::Daemon::Init( \%proc_opts );
  $self->{status}{$self->{id}}{timestamp} = time;
  $self->{status}{$self->{id}}{runcount} = $self->{status}{$self->{id}}{runcount} ? $self->{status}{$self->{id}}{runcount} + 1 : 1;
}

method stop() {
  if ($self->pid) {
    kill 9, $self->pid;
    $self->{status}{$self->{id}}{timestamp} = undef;
    $self->{status}{$self->{id}}{running} = 0;
    $self->{status}{$self->{id}}{pid} = undef;
    $self->clear_pid;
    return 1;
  }
  return 0;
}

method running() {
  #TODO: 0 is false, but it's also a valid process. Re-work the logic here.
  if ( ! $self->pid ) {

  } elsif ( kill 0, $self->pid ) {
    return 1; 
  }
  $self->{status}{$self->{id}}{timestamp} = undef;
  $self->{status}{$self->{id}}{running} = 0;
  $self->clear_pid;
  return 0;
}

1;
